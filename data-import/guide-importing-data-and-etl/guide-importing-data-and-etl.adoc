= Tutorial: Import Relational Data Into Neo4j
:slug: guide-importing-data-and-etl
:level: Beginner
:section: Data Import
:section-link: data-import
:northwind: ../../data/northwind
:sectanchors:
:toc:
:toc-title: Contents
:toclevels: 1

.Goals
[abstract]
This guide will teach you the process for exporting data from a relational database (PostgreSQL) and importing into a graph database (Neo4j).
You will learn how to take data from the relational system and to the graph by translating the schema and using import tools.

.Prerequisites
[abstract]
You should have a basic understanding of the link:/developer/graph-database/#property-graph[property graph model] and know how to link:/developer/guide-data-modeling[model data as a graph].
If you haven't already, link:/download[download^] and install link:/developer/neo4j-desktop/[Neo4j Desktop], so you can follow along with the examples.

[role=expertise]
{level}

[#about-northwind]
== Northwind Introduction

In this guide, we will be using the https://code.google.com/p/northwindextended/downloads/detail?name=northwind.postgre.sql&can=2&q=[NorthWind dataset^], a commonly-used SQL dataset.
This data depicts a typical product sale system, storing and tracking customers, products, customer orders, warehouse stock, shipping, suppliers, and even employees and their sales territories.
Although the NorthWind dataset is often used to demonstrate SQL and relational databases, the data also can be structured as a graph.

An entity-relationship diagram (ERD) of the Northwind dataset is shown below.

image::{img}Northwind_diagram.jpg[]

[#northwind-graph-model]
== Developing a Graph Model

The first thing we will need to do to get data from a relational database into a graph is to translate the relational data model to a graph data model.
Determining how we want to structure tables and rows as nodes and relationships will vary depending on what is most important to your business needs.

[NOTE]
--
For more information on adapting your graph model to different scenarios, check out our link:/developer/modeling-designs/[modeling designs] guide.
--

When deriving a graph model from a relational model, we should keep a couple of general guidelines in mind.

1. A _row_ is a _node_
2. A _table name_ is a _label name_
3. A _join or foreign key_ is a _relationship_

In this dataset, the following graph model serves as a first iteration.

image::{img}northwind_graph_simple.png[width=800]

Notice that we have chosen not to model customers and demographics, shipping information, or employee territories in this model.
You can, of course, create a data model to include these entities, but for now, we will keep to this straightforward graph model.

=== How does the Graph Model Differ from the Relational Model?

* There are no nulls.
** Non existing value entries (properties) are just not present.
** In the relational version, to track the employee hierarchy we have a null entry in the 'ReportsTo' column if they don't report to anybody. In the graph version we just don't define a relationship.
* It describes the relationships in more detail. For example, we know that an employee SOLD an order rather than having a foreign key relationship between the Orders and Employees tables. We could also choose to add more metadata about that relationship, should we wish.
* Either model can be more normalized. For example, addresses have been denormalized in several of the tables, but could have been in a separate table. In a future version of our graph model, we might also choose to separate addresses from the `Customer` entities and create separate `Address` nodes.

[#export-csv]
== Exporting the Data to CSV

Now that we know what we'd like our graph to look like, we need to extract the data from PostgreSQL so we can create it as a graph.
The easiest way to do that is to export the appropriate tables in CSV format.
The PostgreSQL 'copy' command lets us execute a SQL query and write the result to a CSV file, e.g. with `psql -d northwind < export_csv.sql`:

.export_csv.sql
[source, sql]
----
include::{northwind}/export_csv.sql[]
----

[#import-with-cypher]
== Importing the Data using Cypher

After we've exported our data from PostgreSQL, we'll use Cypher's link:{manual}/cypher/#query-load-csv[LOAD CSV] command to transform the contents of the CSV file into a graph structure.

First, create the nodes:

.import_csv.cypher
[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=nodes]
----

Next, we'll create indexes on the just-created nodes to ensure their quick lookup when creating relationships in the next step.

[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=indexes]
----

[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=constraints]
----

As the indexes are created after the nodes are inserted, their population happens asynchronously, so we use `schema await` (a shell command) to block until they are populated.

[source, cypher]
----
schema await
----

Initial nodes and indices in place, we can now create relationships of orders to products and employees.

[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=rels_orders]
----

Next, create relationships between products, suppliers, and categories:

[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=rels_products]
----

Finally we'll create the 'REPORTS_TO' relationship between employees to represent the reporting structure:

[source, cypher]
----
include::{northwind}/import_csv.cypher[tags=rels_employees]
----

You can also run the whole script at once using `bin/neo4j-shell -path northwind.db -file import_csv.cypher`.

The resulting graph should look like this:

image::{img}northwind_graph_sample.png[]

We can now query the resulting graph.

[#query-northwind]
== Querying the Graph

One question we might be interested in is:

.Which Employee had the Highest Cross-Selling Count of 'Chocolade' and Which Product?
[source, cypher]
----
MATCH (choc:Product {productName:'Chocolade'})<-[:PRODUCT]-(:Order)<-[:SOLD]-(employee),
      (employee)-[:SOLD]->(o2)-[:PRODUCT]->(other:Product)
RETURN employee.employeeID, other.productName, count(distinct o2) as count
ORDER BY count DESC
LIMIT 5;
----

Looks like employee No. 1 was very busy!

[format="csv", options="header"]
|===
employee.employeeId,other.productName,count
1,Pavlova,56
1,Camembert Pierrot,56
1,Ikura,55
1,Chang,47
1,Pâté chinois,45
|===

We might also like to answer the following question:

.How are Employees Organized? Who Reports to Whom?
[source, cypher]
----
MATCH path = (e:Employee)<-[:REPORTS_TO]-(sub)
RETURN e.employeeID AS manager, sub.employeeID AS employee;
----

[format="csv", options="header"]
|===
manager,employee
2,1
2,3
2,4
2,5
2,8
5,6
5,7
5,9
|===

Notice that employee No. 5 has people reporting to them but also reports to employee No. 2.

Let's investigate that a bit more:

.Which Employees Report to Each Other Indirectly?
[source, cypher]
----
MATCH path = (e:Employee)<-[:REPORTS_TO*]-(sub)
WITH e, sub, [person in NODES(path) | person.employeeID][1..-1] AS path
RETURN e.employeeID AS manager, sub.employeeID AS employee, CASE WHEN LENGTH(path) = 0 THEN "Direct Report" ELSE path END AS via
ORDER BY LENGTH(path);
----

[format="dsv", options="header"]
|===
e.EmployeeID:sub.EmployeeID:via
2:1:Direct Report
2:3:Direct Report
2:4:Direct Report
2:5:Direct Report
2:8:Direct Report
5:6:Direct Report
5:7:Direct Report
5:9:Direct Report
2:6:[5]
2:7:[5]
2:9:[5]
|===

.How Many Orders were Made by Each Part of the Hierarchy?
[source, cypher]
----
MATCH (e:Employee)
OPTIONAL MATCH (e)<-[:REPORTS_TO*0..]-(sub)-[:SOLD]->(order)
RETURN e.employeeID, [x IN COLLECT(DISTINCT sub.employeeID) WHERE x <> e.employeeID] AS reports, COUNT(distinct order) AS totalOrders
ORDER BY totalOrders DESC;
----

[format="dsv", options="header"]
|===
e.EmployeeID:reports:totalOrders
2:[1,3,4,5,6,7,9,8]:2155
5:[6,7,9]:568
4:[]:420
1:[]:345
3:[]:321
8:[]:260
7:[]:176
6:[]:168
9:[]:107
|===

[#update-northwind-graph]
== Updating the Graph

Now if we wanted to update our graph data, we have to first find the relevant information and then update or extend the graph structures.

// price update
// add new product

.Janet is now reporting to Steven
We need to find Steven first, and Janet and her `REPORTS_TO` relationship.
Then we remove the existing relationship and create a new one to Steven.

[source, cypher]
----
MATCH (mgr:Employee {EmployeeID:5})
MATCH (emp:Employee {EmployeeID:3})-[rel:REPORTS_TO]->()
DELETE rel
CREATE (emp)-[:REPORTS_TO]->(mgr)
RETURN *;
----

//graph_result

This single relationship change is all you need to update a part of the organizational hierarchy.
All subsequent queries will immediately use the new structure.

[#import-northwind-resources]
== Resources
* link:{github}/data/northwind[Northwind SQL, CSV and Cypher data files] link:{github}/data/northwind/northwind.zip[(zip)]
* link:/developer/cypher/guide-sql-to-cypher[From SQL to Cypher]
* link:/developer/working-with-data/guide-import-csv[Import CSV Guide]
* link:/developer/data-modeling/guide-data-modeling[Graph Data Modeling]
* http://neo4j.com/blog/loading-sql-neo4j-like-magic/[Tool: SQL to Neo4j Import]
